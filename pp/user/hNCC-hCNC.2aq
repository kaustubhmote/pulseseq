;hNCC-hCNC.2aq
;$COMMENT=Sequential Acquisition of NCaCx and CoNCa
;$CLASS=Solids-CP
;$DIM=3D
;$TYPE=Multiple-Experiments
;$SUBTYPE=13C-detection

; #----- reference
; Gopinath et al., J. Magn. Reson. (2012) 220, 79-84
; doi:10.1016/j.jmr.2012.04.006

; #---experiment
; 2A2E1R-[3D-NCaCx_f + 3D-CaNCo_f]
; see Ksharma et al., 2016, J Biomol NMR

; #---issues, suggestions?
; kaustuberm [at] tifrh [dot] res [dot] in

; #---ZGOPTNS
; -Dncc: dream transfer for NCaCx expt --> NCaCo or NCaCb
; -Dcncx: darr mixing after CaNCo --> CaN(Co)Cx  
; -Dcncc:  dream mixing after CaNCo ---> CaN(Co)Ca or CoN(Ca)Cb

; #-------------------------------------------#
; #  td1 = 2x(the td1 that you want to use)   #
; #  td2 = 1x(the td2 that you want to use)   # 
; #  This will be corrected internally        #
; #-------------------------------------------#

; #----channels
; f1: 13C
; f2: 1H
; f3: 15N 
; #-----------------------------------------------------

; #---------- pulses        
;p1: 1H 90 pulse @ pl1
;p2: 13C 90 pulse @ pl2
;p3: 15N 90 pulse @ pl3
;p15: H, simcp
;p25: C/N SPECIFIC 
;p26: C/N SPECIFIC CP 2
;mix1: mixing time for NCaCx experiment, set by l1
;mix2: mixing time for CaN(Co)Cx experiment, set by l2
;p31: rCW CW pulse, calculated from cnst31 and p32
;p32: =180 deg pulse for 1H at pl12

; #---------- Power Levels 
;pl1: 1H 90 deg pulse  @ p1 us
;pl2: 13C 90 deg pulse @ p2 us
;pl3: 15N 90 deg pulse @ p3 us
;sp0: initial simcp on 1H 
;pl16: sim cp power level on 13C
;pl17: sim cp power level on 15N
;pl12: Decoupling power on 1H during t1, t2, t3
;pl13: Decoupling power on 1H  SPECIFIC CP/DREAM
;sp2: C, specific cp (N-->CC and C-->NC)
;sp3: C, specific cp (CN-->C)
;pl25: SPECIFIC CP power level on 15N
;sp10: darr for NCC
;sp11: dream for CNcC


; #------------constants and loops
;cnst31: MAS spinning speed
;l1: mixing-time=l1*(1/cnst31) for NCC
;l2: mixing-time=l2*(1/cnst31) for CNcC


; #------ delays and increments
;d1: recycle delay
;d2: indirect increment 1
;d3: indirect increment 2
;in2: =inf1
;in3: =inf2

; #------------- shaped pulses
;spnam0: linear ramp on 1h during CP
;spnam2: adiabatic shapen on 13C for specific cp 1
;spnam3: adiabatic shapen on 13C for specific cp 2
;spnam10: darr mixing (square)
;spnam11: dream mixing for CNcC
; #----------------------------------------------------------------

; #---shape offsets
;SPOFFS0: 0
;SPOFFS2: Ca center
;SPOFFS3: Co center
;SPOFFS10: 0
;SPOFFS11: Ca/Co center in DREAM CNcC

; #--------------decoupling
;cpdprg2: rCW-ApA
; #-----------------------

; #------ bookkeeping and settings
# define H f2
# define C f1
# define N f3

; #---indirect dimension
  "in2 = inf1"
  "in3 = inf2"
  define loopcounter ni
  "ni = td1/4"
  define loopcounter ni2
  "ni2 = td2/2"

  "d2=0.1u"
  "d3=0.1u"

; #------darr mixing times
  define pulse mix1
  "mix1=(1s/cnst31)*l1"
# ifdef cncx
  define pulse mix2
  "mix2=(1s/cnst31)*l2"
# endif
# ifdef cncc
  define pulse mix2
  "mix2=(1s/cnst31)*l2"
# endif

; #---precalc
  define delay OnTime
  "OnTime = 1u"
  define delay t1evo
  "t1evo = ni*in2 + in3*ni2"

; #---default delays
  "d60 = 0.1u"
  "d61 = 0.5u"
  "d62 = 1.0u"
  "d63 = 10u"

; #---for rCWApA decoupling
  "p31 = (1s/cnst31)*0.98 - p32/2"

; #---include
#include <Avancesolids.incl>

; #------ pulse seq starts

1 ze
2 d60 do:H

; #--------------------------------------------------
  d1 
  d63 st0

; #---90 deg on 1H
  d63 pl1:H pl16:C pl17:N
  (p1 ph1):H

; #---sim cp to 13C and 15N
  (p15:sp0 ph20):H (p15 ph16):C (p15 ph17):N

; #---indirct dimension 1
  d61 pl12:H 
  d60 cpds2:H
  d2
  d60 do:H

; #---bidirectional specific cp
  d61 pl21:N pl13:H
  d60 cw:H
  (p25:sp2 ph18):C (p25 ph19):N
  d60 do:H

; #---indirect dimension 2
  d61 pl12:H pl2:C pl3:N
  d60 cpds2:H
  d3
  (p2 ph5):C (p3 ph6):N
  d60 do:H

; #---darr mixing
  (mix1:sp10 ph20):H
  (p2 ph21):C

; #---detect NCaCx
  d61 pl12:H
  d60 cpds2:H
  goscnp ph31 
  d60 do:H

; #---dephase remaining 13C
  6m st
  (p2 ph21):C
  6m st

; #---recall 15N
; #---and
; #---specific cp
  d61 
  d60 cw:H
  (p3 ph23):N
  (p26:sp3 ph13):C (p26 pl26 ph20):N

# ifdef cncx
  (p1 pl1 ph21):C
  d60 do:H
  (mix2:sp10 ph20):H
  (p1 ph23):C
# endif
# ifdef cncc
  (mix2:sp11 ph20):C
  d60 do:H
# endif

# ifndef cncx
#   ifndef cncc
    d60 do:H
#   endif
# endif

; #---detect CaNCo
  d61 pl12:H
  d60 cpds2:H
  go=2 ph31 
  d60 do:H

; #---write data
  1m wr #0 if #0 zd

; #---quadrature loop 1
  d63 ip16
  d63 ip17
  lo to 2 times 2

; #---increment loop 1
  d63 id2
  lo to 2 times ni

; #---quadrature loop 2
  d63 rd2
  d63 ip5
  d63 ip6
  lo to 2 times 2

; #---increment loop 2
  d63 id3
  lo to 2 times ni2

; #---end of pulse sequence
6 exit

; #---phase table
; min=8 step full=16

  ph1 = 1 3
  ph16 = 0 
  ph17 = 0 0 0 0 0 0 0 0 
         2 2 2 2 2 2 2 2 
  ph5 = 3
  ph6 = 1
  ph18 = 0 0 2 2 
  ph19 = 0 0 0 0 2 2 2 2 
  ph13 = 0 0 0 0 0 0 0 0 
         2 2 2 2 2 2 2 2  
  ph31 = 0 2 2 0 2 0 0 2
         2 0 0 2 0 2 2 0

  ph20 = 0
  ph21 = 1
  ph22 = 2
  ph23 = 3

        


