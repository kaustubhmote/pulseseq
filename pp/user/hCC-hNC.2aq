;hCC-hNC.2aq
;$COMMENT=Sequential Acquisition of DARR and NCa
;$CLASS=Solids-CP
;$DIM=2D
;$TYPE=Multiple-Experiments
;$SUBTYPE=13C-detection

; #---reference
; Gopinath et al., Angewandte Chemie (Int. Ed.) 51, 2731-2735 (2012)

; #---experiment
; 2A2E1R-[ 2D-DARR _f + l0 x 2D-NCa_f ]
; See Sharma et al., 2016 J Biomol NMR for nomenclature

; Issues, suggestions?
;    kaustuberm [at] tifrh [dot] res [dot] in

; #---ZGOPTNS
; -Dncx: DARR mixing after NCa
; -Dncc: DREAM transfer after NCa

; #---Notes
; 1. Indirect acquisition mode hard coded to States-TPPI
; #-----------------------------------------------------------#
; #                                                           #
; #   TD1 you put in = 2x(td1-that-you want-to-use)           #
; #     This will be corrcted internally ,.                   #
; #  Additinonally, TD1 that you put in should be a           #
; #     multiple of 4*l0  so that all 15N expts are same      #
; #                                                           #
; #-----------------------------------------------------------#

; #---channels
; f1: 13C
; f2: 1H
; f3: 15N 
; #-----------------------------------------------------

; #---pulses        
;p1: 1H 90 pulse @ pl1
;p2: 13C 90 pulse @ pl2
;p3: 15N 90 pulse @ pl3
;p15: SIMCP from 1H-->13C/15N 
;p25: SPECIFIC CP 15N-->13C or 13C-->15N, contact time
;mix1: mixing time for DARR, PDSD experiment, set by l1
;mix2: mixing time for NCaCx experiment, set by l2
;p32: 1H 180 deg pulse for rCW-ApA dec @ pl12
;p31: CW pulse for rCW-ApA decoupling, calculated from p32 and cnst31
; #--------------------------------------------------

; #---power 
;pl1: 1H 90 deg pulse  @ p1 us
;pl2: 13C 90 deg pulse @ p2 us
;pl3: 15N 90 deg pulse @ p3 us
;sp0: H, simcp
;pl16: C, simcp
;pl17: N,simcp
;sp10: H, DARR mixing power level
;pl12: H, Decoupling power on 1H during t1, t2 
;pl13: H, Decoupling power on 1H  SPECIFIC CP
;sp2: C, SPECIFIC CP 
;pl21: N, SPECIFIC CP 
; #--------------------------------------------------

; #---delays
;d1: recycle delay
;d0: indirect 13C increment
;dN: indirect 15N increment, set internally
;l1: mixing-time=l1*(1/cnst31) for DARR
;l2: mixing-time=l2*(1/cnst31) for NCx  or NCC

; #---shapes
;spnam0: linear ramp
;spnam10: C-C mixing (square for darr)
;spnam2: adiabatic 
;spnam11: darr/dream transfer after NcC

; #---shape frequency offsets
;SPOFFS0:  0
;SPOFFS2: Adjust to 13Ca or 13Co center
;SPOFFS11: Adjust to 13Ca or 13Co center

; #---decoupling
; cpdprg2: 1H high power dec

; #---multiple-experiments settings
;NBL: 2
;cnst13: SW(13C)/Sw(15N) ~ 1-10
;cnst13: Ratio SW (13C)/Sw(15N) ~6-10 for Uniformly labeled samples

; #---constants, counters
;cnst31: MAS spinning speed

;l4: current 13C increment number, set=0 in the beginning
; #------------------------------------------------------------

; #---channels
# define H f2
# define C f1
# define N f3

; #---increment dimension
  "in0=inf1"
  define loopcounter ni
  "ni=td1/4"
  define loopcounter nexptno
  "nexptno=0"
  define loopcounter ninc
  "ninc=0"
  "l4=0"
  define delay dN
  "dN=0.1u"
  "d0=0.1u"

; #------darr mixing times
  define pulse mix1
  "mix1=(1s/cnst31)*l1"
# ifdef ncx
  define pulse mix2
  "mix2=(1s/cnst31)*l2"
# endif
# ifdef ncc
  define pulse mix2
  "mix3=(1s/cnst31)*l2"
# endif

; #---default delays
  "d60 = 0.1u"
  "d61 = 0.5u"
  "d62 = 1.0u"
  "d63 = 10u"

; #---aligining
  "p14 = p15 - p3"

; #---pre-calculations
  define delay acquisition
  "acquisition = 2*aq"
  define delay t1evo
  "t1evo =  ni*in0 + (ni/l0)*(in0*cnst13)"
  define delay OnTime
# ifdef ncc
  "OnTime= acquisition + p15 + p20 + t1evo + p1 + p2*3 + p3*2 + mix3 "
# else
  "OnTime= acquisition + p15 + p20 + t1evo + p1 + p2*3 + p3*2"
# endif

; #-----include
#include <Avancesolids.incl>

; #------pulse sequence starts 
1 ze
2 d61 do:H

; #---determine dN
  "nexptno=l4/(ni/l0)"
  "ninc=l4%(ni/l0)" 
  "dN=cnst13*ninc*in0"

; #---Protection
; #---total acqtime < 50ms
  if "acquisition > 25ms" goto ErrAq50
; #---total T1evolution time < 50ms
  if "t1evo > 30m" goto ErrT130
; #---cp mixing < 10ms
  if "p15 > 10ms" goto ErrCP10
; #---darr mixing 1 < 1s
  if "mix1 > 1s" goto ErrMix1
# ifdef ncx
; #---darr mixing < 100m
  if "mix2 > 100m" goto ErrMix2
# endif
# ifdef ncc
; #---dream mixing < 10m
  if "mix3 > 10m" goto ErrMix3
# endif
  if "OnTime > 0.1*d1" goto ErrDuty
; #---TD1 setting
  if "td1 % (4*l0) != 0" goto ErrTD1

; #---------------------------------------------------------------- 
  d1  ; recycle delay
  d63 st0

; #---90 deg on 1H
  d63 pl1:H pl16:C pl17:N 
  (p1 ph1):H

; #---sim cp to 13C and 15N
  (p15:sp0 ph20):H (p15 ph16):C (p14 ph17 p3 pl3 ph21):N

; #---indirect evolution on 13C
  d61 pl12:H pl2:C
  d60 cpds2:H
  d0
  d60 do:H

; #---DARR mixing on 13C
  (p2 ph5):C
  d60 do:H
  (mix1:sp10 ph20):H
  (p2 ph21):C

; #---detect 13C	
  d61 pl12:H
  goscnp ph31 cpds2:H
  d60 do:H

; #---dephase remaining 13C
  6m st 
  (p2 ph21):C
  6m

  d60 cpds2:H
  (p3 ph6):N    
  dN
  d60 do:H

; #---specific cp from 15N to 13C	
  d61 pl13:H 
  d60 cw:H
  (p20:sp2 ph19):C (p20 ph20):N

# ifdef ncx
  (p2 pl2 ph23):C
  d60 do:H
  (mix2:sp11 ph20):H
  (p2 pl2 ph21):C
  d60 do:H
# endif
# ifdef ncc
  (mix3:sp11 ph20):C
  d60 do:H
# endif

# ifndef ncc
#   ifndef ncx
    d60 do:H
#   endif
# endif

; #---detect 13C	
  d60 pl12:H
  go=2 ph31 cpds2:H
  d60 do:H

; #---write data
  10m wr #0 if #0 zd

; #---quadrature loop
  d63 ip5
  d63 ip6
  lo to 2 times 2

; #---increment loop
  d63 id0
  d63 iu4
  lo to 2 times ni

  goto HaltAcqu

ErrAq50, d63
  print "Total acquisition time exceeds 50ms. Edit pp if you are sure."
  goto HaltAcqu

ErrT130, d63
  print "Total t1 evolution time exceeds 30ms. Edit pp if you are sure."
  goto HaltAcqu

ErrCP10, d63
  print "Total cp time > 10ms. Edit pp if you are sure."
  goto HaltAcqu

ErrMix1, d63
  print "Total darr mixing > 1s. Edit pp if you are sure."
  goto HaltAcqu

ErrMix2, d63
  print "Total nccx mixing > 500m. Edit pp if you are sure."
  goto HaltAcqu

ErrMix3, d63
  print "Total nccx mixing > 500m. Edit pp if you are sure."
  goto HaltAcqu

ErrDuty, d63
  print "Duty cycle exceeds 10%. Edit pp if you are sure."
  goto HaltAcqu

ErrTD1, d63
  print "TD1 must be muliple of 4*l0"
  goto HaltAcqu

HaltAcqu, d63

6 exit

; #---phase table
; min=2, full=8 
  ph1 = 1 3
  ph16 = 0 0 2 2 2 2 0 0 
  ph17 = 0 0 2 2 
  ph5 = 3
  ph6 = 1
  ph19 = 0 0 0 0 2 2 2 2  
  ph31 = 0 2 2 0 2 0 0 2

  ph20 = 0
  ph21 = 1
  ph22 = 2
  ph23 = 3
