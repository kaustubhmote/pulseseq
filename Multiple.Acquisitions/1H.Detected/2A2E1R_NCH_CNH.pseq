; ###########################################################
; ####### 2A2E1R-[ 3D-NCH_f + 3D-CNH_f ] ####################
; ###########################################################

; #--- Reference
; K. Sharma, P. K. Madhu and K. R. Mote,
; J. Biomol. NMR (2016) 
; 

; #----Important Setup Instructions

; 0. Pulse sequence verified on Topspin 2.1 and Avance III Console 

; 1. Windowed 1H detection is used in this pulse sequence
;    Keep the acq mode as analog
;    Otherwise, if you are using fast MAS (> 40 kHz), simply replace all
;    commands tha correspond to windowed acq with normal acq commands.

; 2. If you are using windowed detection, manually set the sw to cnst6
;    This makes sure that all sampled points are actullly stored. 
;    Ensure that TD = 2*count

; 3. This pulse sequences uses different buffers to detect the CH and NH spectra 
;    sequentially. Set "NBL=2" before the start of the experiment, otherwise the spectra will
;    keep overwriting

; 3. Topspin calculates dataset size for a 3D program by TD0*TD1*TD2
;    In this pulse seq, dataset size  = 2*TD0*TD1*TD2
;    This mismatch causes an alert box to pop up and
;    the experiment does not start till this is 'OK'd
;    To avoid this, set TD1=2*(the-TD1-you-would-normally-set)
;    The pulse program will internally correct for that
;    The "REAL" TD1 for each experiments is given by the loopcounters 
;    as follows:  TD1 = 2*ni 

; 4. Processing
;    Use the 'split' command and give the folder number (eg 10001)
;    3D-NCH will be in dataset 10001
;    3D-CNH will be in dataset 10002

;    Alternatives:
;    1.  nmrPipe: Use the COADD function
;       "| nmrPipe -fn COADD -axis Y -cList 1 0 \ " for NCH 
;       "| nmrPipe -fn COADD -axis Y -cList 0 1 \ " for CNH       
;    2. nmrglue/python: numpy array slicing functionality.
;       "data_1 = data[0::2] for NCH
;       "data_2 = data[1::2] for CNH
;    Example processing scripts for nmrPipe and nmrglue are available at
;    www.github.com/kaustubhmote/pulsequences
 
; 5. The indirect acquisition mode is hard-coded to States-TPPI owing to differences
;    in the mc macro statements in different topspin versions. You may replace it or 
;    use as is and always process using the States-TPPI mode

; 6. Issues, suggestions?
;    kaustuberm [at] tifrh [dot] res [dot] in
;    or fork the repository from www.github.com/kaustubhmote/pulseseq


; #----windowed pmlg settings
;l11: 16
;p5: 2.0us (optimize from 1.5 to 2.2us for following changes: probe, SP1, MAS-speed )
;p9: d3 = 1.5us (optimize for each probe)
;sp1: PMLG decoupling power level (usuall 80-90 kHz)
;pl13 : SP1
;d8: time for rec to turn on 0.1-1.6u
; ---------------------------------------------------------------

; #----pulses
;p1: 1H initial 90 deg pulse @ pl1
;p15: Contact time for initial cross-polarization from 1H to 13C and 15N
;p2: 90 deg pulse on 13C @ pl2
;p3: 90 deg pulse on 15N @ pl3
;p23: SPECIFIC CP contact time (bidirectional CP after t1 evolution for 15N and 13C)
;p17: selective (short) CP from 13C to 1H
;p18:  selective (short) CP from 15N to 1H
;p31: pulse for rCW-AiA decoupling, set to 0.98*rotor-cycle
;p32: 180 pulse on 1H @ pl12
; ----------------------------------------------------------------

; #----power levels
;pl1: 1H, 90 pulse @ length p1
;pl2: 13C, 90 pulse @ length p2
;pl3: 15N, 90 pulse @ length p3
;pl7: 1H, water suppression, usually 10-20 kHz
;pl12: 1H, decoupling, 100 kHz
;sp0: 1H, initial CP to 13C and 15N, @ p15 (ramp)
;pl16:	13C, initial CP tfrom 1H, @ p15
;pl17:	15N, initial CP tfrom 1H, @ p15
;sp6: 13C, SPECIFIC CP @ p23 (ramp)
;pl23: 15N. SPECIFIC CP @ p23
;sp3: 1H, selective (short) CP from 13C to 1H @ p17 (ramp)
;pl24: 13C, selective (short) CP from 13C to 1H @ p17 (ramp)
;sp5: 1H, selective (short) CP from 15N to 1H @ p18 (ramp)
;pl25: 15N, selective (short) CP from 15N to 1H @ p18 (ramp)
; ---------------------------------------------------------------

; #----loops, etc
;cnst31 : MAS frequency
; -------------------------


; #----definitions

; #---include files
#include <Avancesolids.incl>	 
#include <Delayssolids.incl>

; #---indirect evolution
"in2=inf1"
"in3=inf2"
define loopcounter ni
"ni=td1/4"		
define loopcounter ni2
"ni2=td2/2"

; #----decoupling
"d31=1/cnst31" ;rCW-AiA
"p31=0.98*d31" ;rCW-AiA
; -----------------------

; #----pmlg acquisition
"sp1=pl13"				
"d9=0.1u*(l11)"	
"d3=p9"		
"p10=10*p5" 	
define delay dX
"dX=d3-d9-0.1u"	
define delay cycle
"cycle=2*(d3 + p10 + d9)" 
define loopcounter count
"count=aq/cycle"		
"blktr2 = 0.7u"		
"anavpt=l11"	
; -------------------------------------------------------


; #----- PULSE SEQUENCE STARTS

1 ze	

2 d1

  10u pl1:f1 st0
  10u reset1:f1 
  STARTADC
  RESETPHASE
  1u rpp10

; first 1H 90 at pl1
  (p1 pl1 ph1):f1	
																						
; Cross-polarization 
  (p15:sp0 ph15):f1 (p15 pl16 ph16):f2 (p15 pl17 ph17):f3			

; Water suppression
  0.1u pl7:f1
  0.1u cpds3:f1											
  (p2 pl2 ph20):f2 (p3 pl3 ph24):f3

  d10

  0.1u do:f1
  0.1u pl12:f1
  0.1u cpds2:f1 			
  (p2 pl2 ph21):f2 (p3 pl3 ph25):f3
  
  d2											

  0.1u do:f1
  0.1u cw:f1

; bidirectional specific cp
  (p23:sp6 ph27):f2 (p23 pl23 ph26):f3
  0.1u do:f2
  0.1u cpds2:f1

; indirect evolution on 13C and 15N
; N"C"H and C"N"H

  d3  
	
  0.1u do:f1

; cp from 13C to 1H and store 15N																				
  (p17:sp3 ph18):f1 (p17 pl24 ph19):f2 (p3 pl3 ph22):f3	

; detection NC"H"
  
4 del
  d8 RG_ON
  sample												
  (p10:sp1 ph10^):f1						
  del  
  d8 RG_ON
  sample
  (p10:sp1 ph10^):f1
  lo to 4 times count					
  100u eoscnp										

; move file pointer to buffer 2
  10u st						

  STARTADC				
  RESETPHASE		
  1u rpp10		


  6m 
  (p1 pl1 ph0):f1		
  6m 

; bring back stored 15N magnetization 
  (p3 pl3 ph23):f3					; 15N 90

; cp from 15N to 1H	
  (p18:sp5 ph28):f1 (p18 pl25 ph29):f3
	

; detection CN"H"
5 del
  d8 RG_ON
  sample						
  (p10:sp1  ph10^):f1	
  del
  d8 RG_ON
  sample
  (p10:sp1  ph10^):f1
  lo to 5 times count	
  100u rcyc=2

  1m wr #0 if #0 zd

  10u ip21
  10u ip25
  lo to 2 times 2

  id2
  lo to 2 times ni
 
  rd2
  10u ip19
  10u ip22
  lo to 2 times 2

  id3
  lo to 2 times ni2



6 exit									; finished



ph0  = 0
ph1  = 1 3

ph15 = 0
ph16 = 0
ph17 = 0

ph10 = 0 2  	 				

ph20 = 3 
ph21 = 1 

ph22 = 1 3 3 1
ph23 = 3 1 1 3 

ph24 = 1    
ph25 = 3    

ph18 = 0 
ph19 = 0

ph27 = 0 0 0 0 2 2 2 2   
ph26 = 0 0 2 2  



ph28 = 0 
ph29 = 0

ph30 = 0
ph31 = 0 2 2 0 2 0 0 2 

