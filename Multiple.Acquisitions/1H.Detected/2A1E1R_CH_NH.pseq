; ###########################################################
; ####### 2A1E1R-[ 2D-CH_f + numN x 2D-NH_f ] ####################
; ###########################################################

; #--- Reference
; K. Sharma, P. K. Madhu and K. R. Mote,
; J. Biomol. NMR (2016) 
; 

; #----Important Setup Instructions

; 0. Pulse sequence verified on Topspin 2.1 and Avance III Console 

; 1. Windowed 1H detection is used in this pulse sequence
;    Keep the acq mode as analog
;    Otherwise, if you are using fast MAS (> 40 kHz), simply replace all
;    commands tha correspond to windowed acq with normal acq commands.

; 2. If you are using windowed detection, manually set the sw to cnst6
;    This makes sure that all sampled points are actullly stored. 
;    Ensure that TD = 2*count

; 3. This pulse sequences uses different buffers to detect the CH and NH spectra 
;    sequentially. Set "NBL=2" before the start of the experiment, otherwise the spectra will
;    keep overwriting

; 3. Topspin calculates dataset size for a 2D program by TD0*TD1
;    In this pulse seq, dataset size  = 2*TD0*TD1
;    This mismatch causes an alert box to pop up and
;    the experiment does not start till this is 'OK'd
;    To avoid this, set TD1=2*(the-TD1-you-would-normally-set)
;    The pulse program will internally correct for that
;    The "REAL" TD1 for each experiments is given by the loopcounters 
;    as follows:  TD1 = 2*ni 

; 4. Processing
;    Use the 'split' command and give the folder number (eg 10001)
;    2D-CH will be in dataset 10001
;    2D-MH will be in dataset 10002

;    Alternatives:
;    1.  nmrPipe: Use the COADD function
;       "| nmrPipe -fn COADD -axis Y -cList 1 0 \ " for CH  
;       "| nmrPipe -fn COADD -axis Y -cList 0 1 \ " for NH        
;    2. nmrglue/python: numpy array slicing functionality.
;       "data_1 = data[0::2] for CH 
;       "data_2 = data[1::2] for NH
;    Example processing scripts for nmrPipe and nmrglue are available at
;    www.github.com/kaustubhmote/pulsequences
 
; 5. The indirect acquisition mode is hard-coded to States-TPPI owing to differences
;    in the mc macro statements in different topspin versions. You may replace it or 
;    use as is and always process using the States-TPPI mode

; 6. Issues, suggestions?
;    kaustuberm [at] tifrh [dot] res [dot] in
;    or fork the repository from www.github.com/kaustubhmote/pulseseq

; #----channels
; f1: 1H
; f2: 13C
; f3: 15N
; #---------------------------------------

; #----pulses
;p1: initial 90 on 1H @ pl1
;p15: contact time for simp cp from 1H to 13C and 15N
;p2: 90 deg pulse on 13C @ pl2
;p3: 90 deg pulse on 15N @ pl3
;p17: selective (short) CP from 13C to 1H
;p18: selective (short) CP from 15N to 1H
; #------------------------------------------------------

; #----power levels
;pl1: 1H, 90 pulse @ length p1
;pl2: 13C, 90 pulse @ length p2
;pl3: 15N, 90 pulse @ length p3
;pl7: 1H, H2O saturation power, usually 20-40 kHz
;pl12: 1H, decoupling power during indirect evolution
;sp0: 1H, initial CP to 13C and 15N, @ p15 (ramp)
;pl16: 13C, initial CP tfrom 1H, @ p15
;pl17: 15N, initial CP tfrom 1H, @ p15
;sp3: 1H, selective (short) CP from 13C to 1H @ p17 (ramp)
;pl21: 13C, selective (short) CP from 13C to 1H @ p17 (ramp)
;sp5: 1H, selective (short) CP from 15N to 1H @ p18 (ramp)
;pl22: 15N, selective (short) CP from 15N to 1H @ p18 (ramp)
;pl13: SP1, wPMLG power level for 13C/15N edited experiment
; #------------------------------------------------------------

; #-----pmlg settings for windowed deetction
;l11: =16, defines anavpt for analog mode of detection
;p5: pmlg pulse length for a single phase
;p9: part of the window
;sp1: pmplg decoupling power level (usually 80-100 kHz)
;pl13: =sp1
;d8: delay to turn on the receiver. set to ~1.6u
; #----------------------------------------------------

; #---delays
;d10: water suppression time

; #----loops, etc
;l5: number of 15N-edited experiments to run for each 13C edited experiment (1-10)
;cnst13: ratio of 13C SW to 15N SW
;in0: indirect dwell time for 13C, note that 15N dwell time is automatically set to cnst13*in0
;l4: indirect loop counter, make sure it is '1' at teh beginning of the program
;cnst7: Change in frq in Hz to move from the initial O1 to the O1 optimal for 1H detection of 13C-edited spectra
;cnst8: Change in frq in Hz to move from the initial O1 to the O1 optimal for 1H detection of 15N-edited spectra
; #--------------------------------------------------------------------------------------


; #-------------------- definitions -----------------------------------------------------

; #---incl files
#include <Avancesolids.incl>
#include <Delayssolids.incl>

; #---pmlg power level
 "sp1=pl13"

; #----sampling delays
 "d9=0.1u*(l11)"

; #---part of the window
 define delay WINDOW
 "WINDOW=p9"

; #---pmlg pulses
 "p10=10*p5"

; #---part of WINDOW other than sampling
  define delay del
  "del =  WINDOW - d9 - d8 - 0.1u"

; #---pmlg cycle time (supercycled pmlg)
  define delay cycle
  "cycle=2*(WINDOW + p10)"

; #---to define acq time
  define loopcounter count
  "count=aq/cycle"

; #---spectra width. manually set the sw to this number
  "cnst6=1/cycle"

; #---transmitter blanking
  "blktr2 = 0.7u"

; #---anavpt for analog mode sampling  
  "anavpt=l11"

; #---indirect dwell time on 13C
 "in0=inf1"

; #---indirect increment on 15N
  define delay dN
  "dN=0.1u"

; #---actual increments
  define loopcounter ni
  "ni=td1/4"

; #----number of 15N experiments
  define loopcounter numN
  "numN=l5"

; #----variable to track of current expt number
  define loopcounter nexptno
  "nexptno=0"

; #----increment counter set to zero initially
  "l4=0"



; #------- PULSE SEQUENCE STARTS 
 
1 ze  
2 d1

  10u pl1:f1  st0     
  10u reset1:f1 
  STARTADC 
  RESETPHASE 
  1u rpp10

; #---first 90 on 1H
  (p1 pl1 ph1):f1

; #---sim cp     
  0.1u pl15:f1 
  (p15:sp0 ph15):f1 (p15 pl16 ph16):f2 (p15 pl17 ph17):f3   

; #---water suppression
  1u pl7:f1 pl2:f2 pl3:f3    
  1u cpds3:f1 
  (p2 pl2 ph20):f2 (p3 pl3 ph22):f3 
  d10 
  0.1u do:f1


  0.1u pl12:f1
  0.1u cpds2:f1

; #--evolve 15N and 13C

; #---determine 15N exptnumber
  "nexptno=(l4)/(ni/numN)"

; #---for the first increment
  if "d0 < in0" goto label8

; #---calculate 15N evolution time
  "dN=cnst13*(d0-nexptno*in0*ni/numN)"

; #---execution for cases where 13C evol. > 15N evol.
  if "(dN + p3) < (d0 + p2)" goto label9

; #---execution for cases where 15N evol. > 13C evol.
  "d4=dN+p3-d0-p2"
  goto label10

; #--- evolve Case I
label8,
  (p3 ph23):f3 
  (p3 ph22):f3 (p2 ph21):f2
  goto label11 

; #---evolve Case 2
label9,
  (p3 ph23):f3 
  (dN p3 ph22):f3 (p2 ph21 d0):f2
  goto label11
 
; #---evolve Case 3
label10,
  (p3 ph23):f3    
  (dN p3 ph22):f3 (d4 p2 ph21):f2 
  goto label11

; #---all escape
label11,   
  0.1u do:f1


; #---cp from 13C to 1H and store 15N
label20,                                                
  (p17:sp3 ph18):f1 (p17 pl21 ph19):f2 

; #---detection 1  
4 del fq=cnst7(bf ppm):f1
  d8 RG_ON
  sample												
  (p10:sp1  ph10^):f1	
  del  
  d8 RG_ON
  sample
  (p10:sp1  ph10^):f1
  lo to 4 times count
  10u fq=0:f1
  100u eoscnp                   

  100u st   

  STARTADC  
  RESETPHASE 
  1u rpp10 

  label30,

  10m 
  (p1 pl1 ph27):f1 
  10m 
    

(p3 pl3 ph24):f3

; #---cp back to 1H from 15N
  (p18:sp5 ph28):f1 (p18 pl22 ph29):f3       
  
; #---detection 2
5 del fq=cnst8(bf ppm):f1
  d8 RG_ON
  sample
  (p10:sp1  ph10^):f1
  del
  d8 RG_ON
  sample
  (p10:sp1  ph10^):f1
  lo to 5 times count
  1u fq=0:f1
  
  100u rcyc=2                 


; #---write 
  1m wr #0 if #0 zd   

; #---quadrature loop STATES TPPI
  1m ip21                
  1m ip23
  lo to 2 times 2    

; #---increment loop
  1m id2
  1m iu4  
  lo to 2 times ni 



6 exit     

; #---phase tables

 
; #--- 2 steps
ph0  = 0
ph1  = 1 3

ph15 = 0
ph16 = 0  
ph17 = 0  

ph10 = 0 2  	 		

ph20 = 3 1 
ph21 = 1 3 

ph22 = 1 3
ph23 = 3 1
ph24 = 3 1


ph18 = 0 
ph19 = 0

ph27 = 1

ph28 = 0  
ph29 = 0

ph30 = 0
ph31 = 0 2 


/*
; #-----4 steps
ph0  = 0
ph1  = 1 3

ph15 = 0
ph16 = 0  
ph17 = 0  

ph10 = 0 2  	 		

ph20 = 3 1 
ph21 = 1 3 

ph22 = 1 3
ph23 = 3 1
ph24 = 3 1

ph18 = 0 0 2 2 
ph19 = 0

ph27 = 1

ph28 = 0 0 2 2
ph29 = 0

ph30 = 0
ph31 = 0 2 2 0
*/

/* ; #-----8 steps
ph0  = 0
ph1  = 1 3

ph15 = 0
ph16 = 0 0 2 2  
ph17 = 0 0 2 2  

ph10 = 0 2  	 		

ph20 = 3 1 1 3
ph21 = 1 3 3 1

ph22 = 1 3 3 1
ph23 = 3 1 1 3
ph24 = 3 1 1 3


ph18 = 0 0 0 0 2 2 2 2  
ph19 = 0

ph27 = 1

ph28 = 0 0 0 0 2 2 2 2 
ph29 = 0

ph30 = 0
ph31 = 0 2 2 0 2 0 0 2
*/ 

 

