; ####################################################################
; ################ 2A2R-[2D-CH_f + 2D-NCa_f] #########################
; ####################################################################



#include <Avancesolids_multrec.incl>
#include <Delayssolids_multrec.incl>


  "sp1=pl13"        
  "d9=0.1u*(l11)"
  "d3=p9" 
  "p10=10*p5" 


  define delay dX
  "dX=d3-d9-0.1u" ; part of the window

  define delay cycle
  "cycle=2*(dX + p10 + d9 + d9 + 0.1u )"


  define loopcounter count
  "count=0.010/cycle" 

  "blktr2 = 0.7u"     
 
  "anavpt=l11"

  "pl15=sp0"

  define loopcounter ni
  "ni=td1/2"

  define loopcounter numN
  "numN=l5"

  define delay dN
  "dN=in2"

  "l4=1"


  "d31=1/cnst31"
  "p31=0.98*d31"

  "in2=inf1"

; -------------------- pulse sequence starts ---------------------

1 ze1
  ze2
2 10u do:f2
  d1

  STARTADC2 
  RESETPHASE2
  1u rpp10

  10u pl17:f1 pl2:f2 pl18:f3

; ---first 90 on 1H
  (p2 ph2):f2                         

; ---SIMP CP
  1u pl15:f2
  (p15 ph1):f1 (p15:sp0 ph20):f2 (p15 ph3):f3       

; ---Water suppression
  1u pl3:f3 pl1:f1 pl7:f2
  0.1u cpds3:f2
  (p3 ph5):f3 (p1 ph7):f1
  d10
  (p1 ph8):f1  
  0.1u do:f2

; ---13C evolution
  0.1u pl12:f2     
  0.1u cpds2:f2
  ; d2
  (center (d2) (p3*2 pl3 ph11):f3)
  0.1u do:f2

; ---13C to 1H CP
  0.1u pl17:f1
  (p17:sp3 ph17):f2 (p17 ph18):f1

; ---change 1H offset to have effective decoupling
  10u fq=cnst6(bf ppm):f2

; ---Acquire 1H windowed acquisition on channel 2
4 dX                                                        
  d9 RG_ON2  
  sample2       
  (p10:sp1  ph10^):f2   
  dX
  d9 RG_ON2
  sample2
  (p10:sp1  ph10^):f2
  lo to 4 times count     

; ---reset 1H offset
  10u fq=0:f2
  1u eoscnp2

; 90 on 15N
  1u pl3:f3 pl12:f2
  1u cpds2:f2
  (p3 ph6):f3  

; ---loop structure for multiple 15N acquisitions starts
; ---find which increment you are currently running

  if "ni <= 1" goto label11

  if "l4 <= 1*ni/numN" goto label0

  if "l4 <= 2*ni/numN & l4 > 1*ni/numN" goto label1

  if "l4 <= 3*ni/numN & l4 > 2*ni/numN" goto label2

  if "l4 <= 4*ni/numN & l4 > 3*ni/numN" goto label3

  if "l4 <= 5*ni/numN & l4 > 4*ni/numN" goto label4

  if "l4 <= 6*ni/numN & l4 > 5*ni/numN" goto label5

  if "l4 <= 7*ni/numN & l4 > 6*ni/numN" goto label6

  if "l4 <= 8*ni/numN & l4 > 7*ni/numN" goto label7

  if "l4 <= 9*ni/numN & l4 > 8*ni/numN" goto label8

  if "l4 <= 10*ni/numN & l4 > 9*ni/numN" goto label9

  goto label11 ; protection

; ---depending on the increment number and l5, 
;    one of the following 10 delays will be executed

label0,
  define delay dN0
  "dN0=cnst13*d2"   
  (center (dN0) (p1*2 pl1 ph21):f1)
  goto label11   ; exp1


label1,
  define delay dN1
  "dN1=cnst13*(d2-1.0*dN*ni/numN)"
  if "dN1 < 2u" goto label11
  (center (dN1) (p1*2 pl1 ph21):f1)
  goto label11   ; exp2

label2,
  define delay dN2
  "dN2=cnst13*(d2-2.0*dN*ni/numN)"
  if "dN2 < 2u" goto label11
  (center (dN2) (p1*2 pl1 ph21):f1)
  goto label11   ; exp3

label3,
  define delay dN3
  "dN3=cnst13*(d2-3.0*dN*ni/numN)"
  if "dN3 < 2u" goto label11
  (center (dN3) (p1*2 pl1 ph21):f1)
  goto label11   ; exp4

label4,
  define delay dN4
  "dN4=cnst13*(d2-4.0*dN*ni/numN)"
  if "dN4 < 2u" goto label11
  (center (dN4) (p1*2 pl1 ph21):f1)
  goto label11   ; exp5

label5,
  define delay dN5
  "dN5=cnst13*(d2-5.0*dN*ni/numN)"
  if "dN5 < 2u" goto label11
  (center (dN5) (p1*2 pl1 ph21):f1)
  goto label11   ; exp6

label6,
  define delay dN6
  "dN6=cnst13*(d2-6.0*dN*ni/numN)"
  if "dN6 < 2u" goto label11
  (center (dN6) (p1*2 pl1 ph21):f1)
  goto label11   ; exp7

label7,
  define delay dN7
  "dN7=cnst13*(d2-7.0*dN*ni/numN)"
  if "dN7 < 2u" goto label11
  (center (dN7) (p1*2 pl1 ph21):f1)
  goto label11   ; exp8

label8,
  define delay dN8
  "dN8=cnst13*(d2-8.0*dN*ni/numN)"
  if "dN8 < 2u" goto label11
  (center (dN8) (p1*2 pl1 ph21):f1)
  goto label11 ; exp9

label9,
  define delay dN9
  "dN9=cnst13*(d2-9.0*dN*ni/numN)"
  if "dN9 < 2u" goto label11
  (center (dN9) (p1*2 pl1 ph21):f1)
  goto label11 ; exp10

label11, ; ----- all loops escape to this position
  1u do:f2


; ---SPECIFIC CP 15N-->Ca
  1u cw:f2
  0.1u pl21:f3
  10u fq=cnst5(bf ppm):f1
  (p7:sp5 ph19):f1 (p7 ph20):f3
  10u fq=0:f1
  0.1u do:f2

; ---digital acquisition of 13C channel 1  
  0.1u cpds2:f2
  ACQ_START1(ph30,ph31)     
  aq1 DWELL_GEN1
  1u do:f2


; ---NS loop
  1u rcyc1=2

; ---write files
  10m wr1 #0 if1 #0 zd1
  10m wr2 #1 if2 #1 zd2

; ---quadrature loop (STATES-TPPI)
  10u ip8
  10u ip6
  lo to 2 times 2

; ---increment loop
  10u id2
  10u id3
  10u iu4
  lo to 2 times ni


; ------------------- phase tables --------------------------
ph1=0
ph2=1 3

ph3=0
ph5=1
ph6=3

ph4=3

ph7=1
ph8=3

ph11=3

ph17=2
ph18=0
ph20=0
ph19=0

ph21=0

ph30=0
ph31=1 3
ph10=0 2

