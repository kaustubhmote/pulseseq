; ######## Heteronucler Correlation Experiment: 1D  version #########

; ############ WINDOWED PMLG SETTINGS #############
; windowed pmlg for the analogue mode, using sharp (standard)
; spnam1=m5m
; shape consists of 10 sclices of length 2*p5/10, set
; shape duration recalculated so slice is a multiple of 100 ns
;	set l11=number of oversampled datapoints to be sampled, 8-32 depending on
;	available window, check acqu in ased for sensible value.
;	l11 was kept at 16
;	the window delay is determined by p9, typical values for probes are as follows
; 4mm BioMAS = 2.8us
; 3.2mm BioMAS = 3.0us
; 2.5mm BioMAS = 3.0us
;	put fw as 6e6 or something very high
;	Parameters to optimise are p9 and p5
;	l11 and d3 (set by p9) together determine the window duration through dX and 'sample'
;	Fix the l11 to 16 and vary only p9
;p5	: 2.0us (optimize from 1.1 to 2.2us for following changes: probe, SP1, MAS-speed )
;sp1	: PMLG decoupling power level (usually 80-100 kHz)
;pl13 : =sp1
; #####################################################

; ######## PULSE SETTINGS #############################
;p1 : 1H initial 90 deg pulse @ pl1
;p15 : Contact time for initial cross-polarization from 1H to 13C and 15N
;p2	:	90 deg pulse on 13C @ pl2
;p17 : selective (short) CP from 13C to 1H	(set to 50us)

; #####################################################

; ######## POWER LEVELS ###############################
;pl1	: 1H, 90 pulse @ length p1
;pl2	: X, 90 pulse @ length p2
;pl7	: 1H, H2O saturation power, usually 10-20 kHz
;pl12	: 1H, decoupling power during indirect evolution
;sp0	: 1H, initial CP to 13C and 15N, @ p15 (ramp)
;pl16	:	X, initial CP tfrom 1H, @ p15
;sp3	: 1H, selective (short) CP from 13C to 1H @ p17 (ramp)
;pl17	: X, selective (short) CP from 13C to 1H @ p17 
;pl13	: SP1, wPMLG power level for X edited experiment
;######################################################

; ######## Processing Constants #######################
;cnst5 : scaling factor, needs to be experimentally determined, usually between 0.3-0.8
;dwell_1H : input dwell time for H direct detection, =(dX+p10+d9+0.1u)
;cnst6 : =1/(dwell_1H), wPMLG RF cycle, = unscaled spectral width 
;cnst7 : scaled spectral width, with the scaling factor of cnst5, =(cnst6/cnst5)
;execute "s swh" and set sw to cnst6
; ####################################################### 



; ######## DEFINITIONS ###############################

#include <Avancesolids.incl>	 
#include <Delayssolids.incl>

	"sp1=pl13"				; sets the shape power level, input being pl13, typically 80-100 kHz
	"d9=0.1u*(l11)"		; set the sampling window in Avancesolids.incl
	"d3=p9"						; defines the widow in conjuction with d9
	"p10=10*p5" 			; wPMLG pulse length
	

define delay dX
	"dX=d3-d9-0.1u"	; part of the window

define delay cycle
	"cycle=2*(dX + p10 + d9 + d9 + 0.1u )" 

define loopcounter count
	"count=aq/cycle"	;make sure td datapoints are sampled

	"blktr2 = 0.7u"		;this opens the transmitter gate 0.7 usec before the
										;pulse, so the transmitter noise is not sample

	"anavpt=l11"			;for analogue mode

define delay dwell_1H
	"dwell_1H = cycle/2"


	"cnst6 = 1/(dwell_1H)"
	"cnst7 = cnst6/cnst5"

"pl17=pl16"

"in0=inf1"

"d31=1/cnst31"
"p31=0.98*d31"


define delay dEV
"dEV = inf1*td1/2 + 1u"



; ######### PULSE SEQUENCE STARTS ####################

 
1 ze	
2	d1 do:f2					;recycle delay


  1u pl1:f1				
  10u reset1:f1 		;synchronise pulse and detection RF
  STARTADC					;prepare adc for sampling, set reference frequency, defined in Avancedru.incl
  RESETPHASE				;reset reference phase
  1u rpp10					;reset phase list pointer


;first 90 at pl1
  p1:f1 ph1					

; Cross-polarization
	(p15:sp0 ph15):f1 (p15 pl16 ph16):f2
 
; Water Suppression

	0.1u pl7:f1				; keep pl7 10-20kHz
	0.1u cpds3:f1			; TPPM works fine
	(p2 pl2 ph20):f2
	d10								; depending on the sample, keep d10 1-2ms (dry samples) and 150-250ms (wet samples)
	(p2 pl2 ph21):f2
	0.1u do:f1

; 2D
	0.1u pl12:f1 
	0.1u cpds2:f1

;	(center (d0) (p3 pl3 ph0):f3)

  d0
	
	0.1u do:f1


; Reverse Cross-polarization
	(p17:sp3 ph18):f1 (p17 pl17 ph19):f2			; generally keep p17 short

; Windowed Detection
4 dX 											; includes reciever dead time and remaining part of window
	d9 RG_ON									; turns on reciever gain
  sample										; takes one complex data point, includes an inbuilt delay of "d9+0.1u", also executes RG_OFF
  (p10:sp1  ph10^):f1				; shape sp1 = m5m
  dX 
	d9 RG_ON
  sample
  (p10:sp1  ph10^):f1
  lo to 4 times count				; make sure td points are sampled.
                
  1u rcyc=2 								; next scan
	
	30m mc #0 to 2 F1PH(ip21,id0)  
	


6 exit									;finished



ph0=0 

ph1=1 3  
ph15 = 0
ph16 = 1				 

ph20 = 3
ph21 = 1 1 3 3 

ph18 = 2 2 2 2 0 0 0 0 
ph19 = 0

ph10=0 2  ; 0 2 for mmbar 

ph30=0
ph31=0 2 2 0 2 0 0 2 
 

